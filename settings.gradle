import org.gradle.api.initialization.resolve.RepositoriesMode

pluginManagement {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "com.atkinsondev.object-store-cache" version "1.4.0"
}

dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

boolean isCI = Boolean.valueOf(System.getenv("GITHUB_ACTIONS"))
String cacheAccessKey = System.getenv("CACHE_ACCESS_KEY") ?: properties['cache_access_key']
String cacheSecretKey = System.getenv("CACHE_SECRET_KEY") ?: properties['cache_secret_key']

buildCache {
    local {
        enabled = !isCI
    }
    remote(com.atkinsondev.cache.ObjectStoreBuildCache) {
        endpoint = 'sfo2.digitaloceanspaces.com'
        accessKey = cacheAccessKey
        secretKey = cacheSecretKey
        bucket = 'maestroplugincache'
        autoCreateBucket = true
        expirationInDays = 10
        push = isCI
    }
}
